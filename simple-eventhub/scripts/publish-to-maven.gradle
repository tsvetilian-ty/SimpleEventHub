
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId SimpleEventHubConfig.GROUP_ID
                artifactId SimpleEventHubConfig.ARTIFACT_NAME
                version SimpleEventHubConfig.VERSION

                from components.release
                artifact androidSourcesJar

                pom {
                    name = SimpleEventHubConfig.ARTIFACT_NAME
                    description = 'SimpmleEventHub is a lifecycle-aware pub/sub event system for Android.'
                    url = 'https://github.com/tsvetilian-ty/SimpleEventHub'

                    developers {
                        developer {
                            name = 'Tsvetilian Yankov'
                            url = 'https://github.com/tsvetilian-ty'
                        }
                    }

                    scm {
                        connection = 'https://github.com/tsvetilian-ty/SimpleEventHub.git'
                        developerConnection = 'https://github.com/tsvetilian-ty/SimpleEventHub.git'
                        url = 'https://github.com/tsvetilian-ty/SimpleEventHub'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/tsvetilian-ty/SimpleEventHub/blob/main/LICENSE.md'
                        }
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            System.getenv('SIGNING_KEY_ID'),
            System.getenv("SIGNING_KEY"),
            System.getenv('SIGNING_PASSPHRASE'),
    )
    sign publishing.publications
}
